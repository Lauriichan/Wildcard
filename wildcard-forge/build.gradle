buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'

version = project.mod_version + '.' + project.mod_patch
group = project.maven_group
archivesBaseName = project.archives_base_name

java.toolchain.languageVersion = JavaLanguageVersion.of(11)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()
}

minecraft {
    mappings channel: 'official', version: '1.16.5'
}

compileJava {
    options.release = 8
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.20'
    
    shade 'me.lauriichan.minecraft.wildcard:wildcard-core:' + project.mod_version
    shade 'mysql:mysql-connector-java:6.0.6'
    shade 'org.xerial:sqlite-jdbc:3.32.3.3'
    shade 'commons-codec:commons-codec:1.15'
    shade 'commons-io:commons-io:2.11.0'
    shade 'org.slf4j:slf4j-api:1.7.30'
    shade 'com.zaxxer:HikariCP:4.0.3'
    
    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
}

mixin {
    add sourceSets.main, "wildcard.mixins.refmap.json"

    config "wildcard.mixins.json"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
        include 'META-INF/mods.toml'
        expand (
                'pluginVersion': project.version
        )
    }

    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
        exclude 'META-INF/mods.toml'
    }
}

shadowJar {
    configurations = [project.configurations.shade]

    relocate 'com.syntaxphoenix.syntaxapi', 'me.lauriichan.minecraft.wildcard.shaded.syntaxapi'
    relocate 'org.apache.commons.io', 'me.lauriichan.minecraft.wildcard.shaded.commons.io'
    relocate 'org.apache.commons.codec', 'me.lauriichan.minecraft.wildcard.shaded.commons.codec'
    relocate 'org.slf4j', 'me.lauriichan.minecraft.wildcard.shaded.slf4j'
    relocate 'com.zaxxer.hikari', 'me.lauriichan.minecraft.wildcard.shaded.hikari'
    relocate 'com.mysql', 'me.lauriichan.minecraft.wildcard.shaded.mysql'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    
    archiveFileName="${baseName}-${version}.${extension}"
}

jar.finalizedBy('shadowJar')
shadowJar.finalizedBy('reobfJar')

artifacts {
    archives shadowJar
    shadow shadowJar
}

reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.outputs.files.singleFile
    }
}