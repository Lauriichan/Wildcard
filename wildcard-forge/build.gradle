buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

version = '2.0.0'
group = 'me.lauriichan.minecraft.wildcard'
archivesBaseName = 'wildcard-forge'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()
}

minecraft {
    mappings channel: 'official', version: '1.16.5'
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.20'
    
    shade 'me.lauriichan.minecraft.wildcard:wildcard-core:2.0.0'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
        include 'META-INF/mods.toml'
        expand (
                'pluginVersion': project.version
        )
    }

    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
        exclude 'META-INF/mods.toml'
    }
}

shadowJar {
    configurations = [project.configurations.shade]

    relocate 'com.syntaxphoenix.syntaxapi', 'me.lauriichan.minecraft.wildcard.shaded.syntaxapi'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
}

jar.finalizedBy('shadowJar')
shadowJar.finalizedBy('reobfJar')

artifacts {
    archives shadowJar
    shadow shadowJar
}

reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.outputs.files.singleFile
    }
}